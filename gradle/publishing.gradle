buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.2'
    }
}

apply plugin: 'maven-publish'
apply plugin: com.jfrog.bintray.gradle.BintrayPlugin

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact groovydocJar

            pom.withXml {
                def root = asNode()
                root.appendNode('name', 'gradle-artifactory-rest-plugin')
                root.appendNode('description', 'Gradle plugin for working with Artifactorys REST API.')
                root.appendNode('url', 'https://github.com/cdancy/gradle-artifactory-rest-plugin')
                root.appendNode('inceptionYear', '2016')

                def scm = root.appendNode('scm')
                scm.appendNode('url', 'https://github.com/cdancy/gradle-artifactory-rest-plugin')
                scm.appendNode('connection', 'scm:https://cdancy@github.com/cdancy/gradle-artifactory-rest-plugin.git')
                scm.appendNode('developerConnection', 'scm:git://github.com/cdancy/gradle-artifactory-rest-plugin.git')

                def license = root.appendNode('licenses').appendNode('license')
                license.appendNode('name', 'The Apache Software License, Version 2.0')
                license.appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0.txt')
                license.appendNode('distribution', 'repo')

                def developers = root.appendNode('developers')
                def cdancy = developers.appendNode('developer')
                cdancy.appendNode('id', 'cdancy')
                cdancy.appendNode('name', 'Christopher Dancy')
            }
        }
    }
}

bintray {
    user = resolveProperty('BINTRAY_USER', 'bintrayUser')
    key = resolveProperty('BINTRAY_KEY', 'bintrayKey')
    publications = ['mavenJava']

    pkg {
        repo = 'gradle-plugins'
        name = 'com.github:gradle-artifactory-rest-plugin'
        desc = 'Gradle plugin for working with Artifactorys REST API.'
        websiteUrl = "https://github.com/cdancy/${project.name}"
        issueTrackerUrl = "https://github.com/cdancy/${project.name}/issues"
        vcsUrl = "https://github.com/cdancy/${project.name}.git"
        licenses = ['Apache-2.0']
        labels = ['gradle', 'artifactory', 'rest', 'plugin', 'gradle-artifactory-rest-plugin']
        publicDownloadNumbers = true

        version {
            vcsTag = "v$project.version"
            attributes = ['gradle-plugin': ["gradle-artifactory-rest-plugin"]]

            gpg {
                sign = true
                passphrase = resolveProperty('GPG_PASSPHRASE', 'gpgPassphrase')
            }
        }
    }
}

String resolveProperty(String envVarKey, String projectPropKey) {
    String propValue = System.getenv()[envVarKey]
    if(propValue) { return propValue }
    hasProperty(projectPropKey) ? getProperty(projectPropKey) : null
}
